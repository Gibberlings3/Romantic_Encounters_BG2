// First Nookie

IF
See(Player1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("RE_RibaldMorningAfter","LOCALS",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Old Ribald's Special Speciality Volume 1

IF
Gender(Player1,FEMALE)
CheckStatGT(Player1,12,CHR)
Global("RE_RibaldRomanceActive","GLOBAL",1)
Global("RE_RibaldSpecialTalk","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("RE_RibaldTimer","GLOBAL",1800)
SetGlobal("RE_RibaldSpecialTalk","GLOBAL",1)
END

IF
See(Player1)
RealGlobalTimerExpired("RE_RibaldTimer","GLOBAL")
Global("RE_RibaldRomanceActive","GLOBAL",1)
CombatCounter(0)
!See([ENEMY])
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
OR(4)
Global("RE_RibaldSpecialTalk","GLOBAL",1)
Global("RE_RibaldSpecialTalk","GLOBAL",3)
Global("RE_RibaldSpecialTalk","GLOBAL",5)
Global("RE_RibaldSpecialTalk","GLOBAL",7)
THEN
RESPONSE #100
IncrementGlobal("RE_RibaldSpecialTalk","GLOBAL",1)
END

IF
See(Player1)
RealGlobalTimerExpired("RE_RibaldTimer","GLOBAL")
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
OR(4)
Global("RE_RibaldSpecialTalk","GLOBAL",2)
Global("RE_RibaldSpecialTalk","GLOBAL",4)
Global("RE_RibaldSpecialTalk","GLOBAL",6)
Global("RE_RibaldSpecialTalk","GLOBAL",8)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Decision

IF
See(Player1)
Global("RE_RibaldDecision","LOCALS",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Old Ribald's Special Speciality Reloaded

// Let's talk about ye and me

IF
See(Player1)
RealGlobalTimerExpired("RE_RibaldTimer","GLOBAL")
Global("RE_RibaldRomanceActive","GLOBAL",2)
Global("RE_RibaldDump","GLOBAL",0)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(2)
Global("RE_RibaldSpecialTalk","GLOBAL",9)
Global("RE_RibaldSpecialTalk","GLOBAL",11)
THEN
RESPONSE #100
IncrementGlobal("RE_RibaldSpecialTalk","GLOBAL",1)
END

IF
See(Player1)
RealGlobalTimerExpired("RE_RibaldTimer","GLOBAL")
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
OR(2)
Global("RE_RibaldSpecialTalk","GLOBAL",10)
Global("RE_RibaldSpecialTalk","GLOBAL",12)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Final Talk

IF
See(Player1)
GlobalGT("Chapter","GLOBAL",%bg2_chapter_5%)
RealGlobalTimerExpired("RE_RibaldTimer","GLOBAL")
Global("RE_RibaldRomanceActive","GLOBAL",2)
Global("RE_RibaldDump","GLOBAL",0)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("RE_RibaldSpecialTalk","GLOBAL",13)
THEN
RESPONSE #100
IncrementGlobal("RE_RibaldSpecialTalk","GLOBAL",1)
END

IF
See(Player1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
RealGlobalTimerExpired("RE_RibaldTimer","GLOBAL")
Global("RE_RibaldSpecialTalk","GLOBAL",14)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Time to say goodbye

// Decision

IF
See(Player1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("RE_RibaldGoodbye","LOCALS",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END